#===============================================================================
# @brief    Scons make file
# Copyright (c) 2018 Huawei Technologies Co. Ltd
#===============================================================================

# build the structure definitions
Import('env')
import EnvironmentUtils

# not yet possible to use common part of the ARM build env used for cores
# so set what is needed here for preprocessing the structure defs.
env = env.Clone(
    tools=['gcc', 'textfile'],
    CC='arm-none-eabi-gcc',
    CPPDEFINES=['COMPILE_FOR_DSL'],
    CPPPATH=['#', '#src/lib/common_headers'],
)

# dummy C file that includes all the required headers to get structure defs.
lines = []
for src_variant in (['neul'] + env['ADDITIONAL_SRC']):
    srcvarenv = env.Clone(SRC_VARIANT=src_variant)
    for inc in EnvironmentUtils.CORE_RELATIVE_FOLDERS[env['CORE']]:
        for header in srcvarenv.Clone(INC=inc).Glob('$SRC_VARIANT/$INC/*.h') + srcvarenv.Clone(INC=inc).Glob('$SRC_VARIANT/$INC/*/*.h'):
            lines.append('#include "%s"\n' % header)

env.Textfile('${CORE}.c', lines)

# preprocess the dummy C file
etypes = env.Command('${CORE}.etypes', '${CORE}.c',
    '$CC -o $TARGET -E $_CPPDEFFLAGS $_CPPINCFLAGS  $SOURCES',
    source_scanner=SourceFileScanner,
)

Return('etypes')
